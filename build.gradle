plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

group 'com.rfw.hotkey_server'

ext {
    // version numbers
    majorVersion = 1
    minorVersion = 0
    patchVersion = 0
    preRelease = "beta"
}

version generateVersionName()

mainClassName = "com.rfw.hotkey_server.Main"

sourceCompatibility = 11.0
targetCompatibility = 11.0

String osName = System.getProperty('os.name').toLowerCase().split()[0]

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    flatDir { dirs 'libs' } // local lib folder
}

javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'com.google.code.findbugs:jsr305:3.0.2' // debugging help library (Nullable, NonNull, etc.)

    compile 'com.jfoenix:jfoenix:9.0.8' // material design library for javaFX
    compile 'org.json:json:20190722' // JSON handling
    compile 'org.imgscalr:imgscalr-lib:4.2' // image processing
    compile 'com.github.kenglxn.QRGen:javase:2.6.0' // QR code generation

    // bluetooth support
    // use different library versions for different OS
    switch (osName) {
        case 'windows':
            // v2.1.1 contains x64 dll files (for 64 bit windows support)
            implementation name: 'bluecove-2.1.1'
            break
        case 'linux':
            // v2.1.0 contains .so files (for linux support)
            implementation name: 'bluecove-2.1.0'
            implementation name: 'bluecove-gpl-2.1.0'
            break
        default:
            throw new Exception('Unknown OS')
    }
}

// for creating normal jar
jar {
    from ('libs/native') { // copy native lib to jar
        include '*'
    }
    manifest {
        attributes "Main-Class": mainClassName
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// for creating fat jar
shadowJar {
    from ('libs/native') { // copy native lib to jar
        include '*'
    }
}

// for creating exe
launch4j {
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    icon = "${projectDir}/icons/hotkey_icon.ico"
    companyName = "rfw"
    outfile = project.name + "-v" + generateVersionName() + ".exe"
}

// make sure jar is build before building exe
createExe.dependsOn shadowJar

run {
    // setting up native library load path when run from IDE
    systemProperty "java.library.path", file("$projectDir/libs/native").absolutePath
}

// method from generating the version name (from version numbers)
def generateVersionName() {
    String versionName = "${majorVersion}.${minorVersion}.${patchVersion}"
    if (preRelease != null && !preRelease.isEmpty()) {
        versionName = versionName + "-" + preRelease
    }
    return versionName
}
